dnl Process this file with 'autoconf' to produce a 'configure' script
dnl $Header: /users/source/archives/td_lib.vcs/RCS/configure.in,v 12.41 1994/12/16 13:02:35 tom Exp $
AC_REVISION($Revision: 12.41 $)
AC_INIT(include/td_lib.h)

###	program paths
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
TD_RCS_SCCS
AC_PROGRAM_PATH(MAKE,make,make)
AC_PROGRAM_PATH(RANLIB,ranlib,echo)
AC_PROGRAM_CHECK(OnHost, uname, [`uname -a`])

###	extra things that we'll substitute in the makefile
AC_SUBST(CC_OPTS)
AC_SUBST(INCLUDES)
AC_SUBST(MAKE)
AC_SUBST(RANLIB)

###	use option -with-warnings to turn on all gcc warnings
CC_OPTS="$CFLAGS"
if test -n "$GCC"
then
AC_WITH(warnings,
[CC_OPTS="$CC_OPTS -Wall -Wshadow -Wconversion -Wstrict-prototypes -Wmissing-prototypes"])
fi

AC_CONST
AC_STDC_HEADERS
AC_DIR_HEADER
AC_MAJOR_HEADER
AC_RETSIGTYPE
TD_SIG_ARGS
TD_REGEX
TD_ANSI_CPP
TD_STAT_ST_BLOCKS

### This may alter include-path
TD_CURSES_LIBS
dnl:AC_HAVE_LIBRARY(curses)
dnl:AC_HAVE_LIBRARY(termcap)

AC_HAVE_HEADERS(stdlib.h unistd.h limits.h string.h memory.h pwd.h)
AC_HAVE_HEADERS(stdarg.h varargs.h)
AC_HAVE_HEADERS(file.h fcntl.h ioctl.h)
dnl don't combine <file.h> and <sys/file.h>, because autoconf gets confused
AC_HAVE_HEADERS(sys/file.h sys/fcntl.h sys/ioctl.h sys/param.h)
AC_HAVE_HEADERS(termcap.h termios.h termio.h sgtty.h utime.h)

AC_HAVE_LIBRARY(bsd, [LIBS="$LIBS -lbsd"])

TD_WAIT
TD_SIZE_T
TD_CURSES_FUNCS

# We're done looking for libraries and header files.  Find the functions and
# datatypes

dnl Functions (use TD_HAVE_FUNCS for debugging, AC_HAVE_FUNCS for quick config)
AC_WITH(warnings,
[
TD_HAVE_FUNCS(
_exit \
_filbuf \
_flsbuf \
calloc \
closedir \
creat \
exit \
fclose \
fflush \
fgetc \
fprintf \
fputc \
fputs \
fread \
free \
fscanf \
fseek \
fwrite \
getenv \
getgid \
getuid \
ioctl \
malloc \
open \
opendir \
pclose \
perror \
popen \
printf \
puts \
readdir \
readlink \
realloc \
rename \
rewind \
rewinddir \
seekdir \
sprintf \
sscanf \
strtok \
strtol \
symlink \
system \
telldir \
time \
ungetc \
)
])

# Functions whose return type is important, or whose existence must be known
# to drive ifdefs.
TD_HAVE_FUNCS(
beep \
bzero \
endgrent \
endpwent \
execvp \
fork \
getcwd \
getegid \
geteuid \
getgrent \
getgrgid \
getgrnam \
getgroups \
getopt \
getpwent \
getpwnam \
getpwuid \
gettimeofday \
getwd \
has_colors \
intrflush \
keypad \
lstat \
ltostr \
memmove \
mkstemp \
mktemp \
putenv \
setbuf \
setegid \
setgrent \
setlinebuf \
setpwent \
setrgid \
setruid \
strchr \
strerror \
strrchr \
tcgetattr \
tgetent \
tgetnum \
tolower \
toupper \
utime \
vfork \
wait \
)

### DataItems/Structs
TD_SYS_ERRLIST
TD_GMTOFF

### DataTypes
TD_DEV_T
TD_INO_T
TD_MODE_T
AC_OFF_T
AC_UID_T
AC_GETGROUPS_T
AC_VFORK
AC_TIME_WITH_SYS_TIME
TD_PROGRAM_FULLPATH(DEFAULT_EDITOR, vi $EDITOR)
TD_PROGRAM_FULLPATH(DEFAULT_BROWSE, view $BROWSE)
TD_PROGRAM_FULLPATH(DEFAULT_PAGER, more $PAGER less most)

###	output config.h
#
#	This bypasses the normal autoconf process because we're generating an
#	arbitrary number of NEED_xxxx definitions with the TD_HAVE_FUNCS macro. 
#	Rather than populate an aclocal.h file with all of those definitions,
#	we do it here.
#
###
changequote({,})dnl
AC_OUTPUT(td_make,
{
if ( cmp -s td_make support/td_lib.mk 2>/dev/null )
then
	echo "support/td_lib.mk is unchanged"
	rm -f td_make
else
	echo creating support/td_lib.mk
	rm -f support/td_lib.mk
	mv td_make support/td_lib.mk
fi
if test -f confdefs.h
then
	rm -f config.h
	echo '/* generated by configure-script */' >config.h
	if test -n "$OnHost"
	then
		echo '/* on host: '"$OnHost"' */' >>config.h
	fi
	cat confdefs.h >>config.h
	if ( cmp -s config.h include/config.h 2>/dev/null )
	then
		echo "include/config.h is unchanged"
		rm -f config.h
	else
		echo "creating include/config.h"
		rm -f include/config.h
		mv config.h include
	fi
else
	echo "Re-run 'configure' to recreate include/config.h"
fi
}
)
changequote([,])dnl
