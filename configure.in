dnl Process this file with 'autoconf' to produce a 'configure' script
dnl $Id: configure.in,v 12.119 2004/03/07 15:35:27 tom Exp $
AC_REVISION($Revision: 12.119 $)
AC_PREREQ(2.13.20020210)
AC_INIT(include/td_lib.h)
AC_CONFIG_HEADER(include/td_config.h:include/config.hin)
CF_CHECK_CACHE

cf_CFLAGS="$CFLAGS"

###	program paths
dnl AC_EXEEXT
dnl AC_OBJEXT
AC_PROG_CC
AC_PROG_CPP
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET
CF_RCS_SCCS
AC_CHECK_PROG(OnHost, uname, [`uname -a`])
AC_CHECK_PROGS(LINT, tdlint lint alint, [echo lint not available])

AC_SYS_LONG_FILE_NAMES
CF_MIXEDCASE_FILENAMES
CF_LOCALE

###	get special options
CF_DISABLE_ECHO
CF_WITH_WARNINGS
AC_ARG_ENABLE(debug,
[  --enable-debug          turn on debug (-g) (for testing/porting)],
[WithDebug=$enableval], [WithDebug=no])
AC_ARG_ENABLE(prototypes,
[  --enable-prototypes     turn on all prototypes (for testing only)],
[WithPrototypes=$enableval], [WithPrototypes=no])

###	undo the -g option of CFLAGS unless we've enabled it
if test -z "$cf_CFLAGS"; then
	if test "$WithDebug" = "no"; then
		CF_STRIP_G_OPT(CFLAGS)
	fi
fi

###	extra things that we'll substitute in the makefile
CF_TOP_SRCDIR
AC_SUBST(INCLUDES)
CF_MAKEFLAGS
CF_MAKE_AR_RULES
CF_MAKE_INCLUDE

### Compiler characteristics
AC_CONST
CF_ANSI_CC_REQS
CF_PROG_EXT
CF_LIB_PREFIX

### Headers
CF_XOPEN_SOURCE
AC_STDC_HEADERS
AC_HEADER_DIRENT
AC_MAJOR_HEADER
AC_RETSIGTYPE
CF_TD_SIG_ARGS
CF_ANSI_CPP
CF_STAT_ST_BLOCKS

AC_HAVE_HEADERS(time.h sys/time.h)
AC_HEADER_TIME

AC_HAVE_HEADERS(\
fcntl.h \
file.h \
grp.h \
ioctl.h \
limits.h \
memory.h \
pwd.h \
sgtty.h \
stdarg.h \
stdlib.h \
string.h \
sys/fcntl.h \
sys/file.h \
sys/ioctl.h \
sys/param.h \
termio.h \
termios.h \
unistd.h \
utime.h \
varargs.h \
)

AC_CHECK_LIB(bsd, bcopy)

# Check for curses libraries, but after getting them, split off the pieces so
# we don't pick up bogus dependencies on shared libraries in the programs that
# don't need curses.
CF_WITH_CURSES_DIR
td_save_LIBS="$LIBS"

AC_ARG_WITH(ncursesw,
[  --with-ncursesw         check for wide-character ncurses],
[WithNcursesW=$withval],  [WithNcursesW=no])

AC_ARG_WITH(ncurses,
[  --with-ncurses          check for ncurses (a SysVr4 curses clone)],
[WithNcurses=$withval],  [WithNcurses=no])

CURSES_LIBS=""
if test "$WithNcursesW" != no ; then
	CF_NCURSES_CPPFLAGS(ncursesw)
	CF_NCURSES_LIBS(ncursesw)
elif test "$WithNcurses" != no ; then
	CF_NCURSES_CPPFLAGS
	CF_NCURSES_LIBS
else
	CF_CURSES_CPPFLAGS
	CF_CURSES_LIBS
fi
CF_CURSES_TERM_H
CF_CURSES_TERMCAP
if test "$LIBS" != "$td_save_LIBS" ; then
	CURSES_LIBS=`echo ".$LIBS" | sed -e "s%.%%" -e "s%$td_save_LIBS\$%%"`
fi
AC_SUBST(CURSES_LIBS)

CF_CHECK_REGEX
CF_VARARGS

### DataTypes
AC_CHECK_TYPE(daddr_t, unsigned long)
AC_CHECK_TYPE(dev_t, unsigned short)
AC_CHECK_TYPE(ino_t, unsigned short)
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_UID_T
AC_TYPE_GETGROUPS
AC_FUNC_VFORK
CF_WAIT
CF_SIZE_T
CF_CURSES_DATA

### DataItems/Structs
CF_SYS_ERRLIST
CF_GMTOFF

# We're done looking for libraries and header files.  Find the functions and
# datatypes

dnl Functions (use CF_HAVE_FUNCS for debugging, AC_HAVE_FUNCS for quick config)
if test "$WithWarnings" = yes; then
CF_HAVE_FUNCS(
_exit \
_filbuf \
_flsbuf \
calloc \
chgrp \
closedir \
creat \
exit \
fclose \
fflush \
fgetc \
fprintf \
fputc \
fputs \
fread \
free \
fscanf \
fseek \
fwrite \
getenv \
getgid \
getuid \
ioctl \
malloc \
open \
opendir \
pclose \
perror \
popen \
printf \
puts \
readdir \
readlink \
realloc \
rewind \
rewinddir \
seekdir \
sprintf \
sscanf \
strtok \
strtol \
symlink \
system \
telldir \
time \
ungetc \
)
fi

# Functions whose return type is important, or whose existence must be known
# to drive ifdefs.
CF_FUNC_LSTAT
CF_CURSES_FUNCS( \
addchnstr \
beep \
cbreak \
erasechar \
getbegx \
getbegy \
getbegyx \
getmaxx \
getmaxy \
getmaxyx \
killchar \
newterm \
putp \
tigetstr \
ungetch \
winnstr \
wresize \
wscrl \
wsetscrreg \
)
CF_CURSES_MOUSE
CF_HAVE_FUNCS(
bzero \
chown \
endgrent \
endpwent \
execvp \
fork \
getcwd \
getegid \
geteuid \
getgrent \
getgrgid \
getgrnam \
getgroups \
gethostname \
getopt \
getpwent \
getpwnam \
getpwuid \
gettimeofday \
getwd \
has_colors \
intrflush \
is_xterm \
keypad \
link \
memmove \
mkstemp \
mktemp \
mktime \
putenv \
realpath \
rename \
resizeterm \
setbuf \
setegid \
seteuid \
setgrent \
setlinebuf \
setpwent \
setrgid \
setruid \
setvbuf \
strchr \
strerror \
strrchr \
tcgetattr \
tgetent \
tgetnum \
toascii \
tolower \
touchwin \
toupper \
ttyname \
typeahead \
use_default_colors \
utime \
vfork \
wait \
)

### Program locations
CF_PROGRAM_FULLPATH(DEFAULT_EDITOR, $EDITOR vi vile ed vim)
CF_PROGRAM_FULLPATH(DEFAULT_BROWSE, $BROWSE view [vile -V] ed)
CF_PROGRAM_FULLPATH(DEFAULT_PAGER, $PAGER more less most pg)

CF_TD_SRC_MODULES
CF_TD_TEST_MODULES

### Chop the curses libraries out of the list.  (We may get into trouble with
### the -L option, or order dependencies on some obscure systems).
if test -n "$CURSES_LIBS" ; then
	LIBS=`echo ".$LIBS" | sed -e "s%.%%" -e "s%$CURSES_LIBS%%"`
fi

AC_OUTPUT( \
	td_make \
	makefile \
	src/makefile \
	$cf_cv_src_makefiles \
	$cf_cv_test_makefiles \
	test/makefile,[
CF_OUTPUT_IF_CHANGED(td_make, support/td_lib.mk)
CF_TD_SRC_MAKEFILES
CF_TD_TEST_MAKEFILES
],[
	AWK="$AWK"
	verbose="$verbose"
	cf_cv_ar_rules="$cf_cv_ar_rules"
	cf_cv_src_modules="$cf_cv_src_modules"
	cf_cv_test_modules="$cf_cv_test_modules"
	make_include_left="$make_include_left"
	make_include_right="$make_include_right"
],sort -u)
