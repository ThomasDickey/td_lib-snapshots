dnl Process this file with 'autoconf' to produce a 'configure' script
dnl $Id: configure.in,v 12.155 2024/04/21 20:15:08 tom Exp $
AC_REVISION($Revision: 12.155 $)
AC_PREREQ(2.52.20200802)
AC_INIT(include/td_lib.h)
AC_CONFIG_HEADER(include/td_config.h:include/config.hin)
CF_CHECK_CACHE

cf_CFLAGS="$CFLAGS"

AC_CANONICAL_TARGET

###	program paths
CF_PROG_CC
AC_PROG_CPP
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET
CF_RCS_SCCS
CF_PROG_LINT
CF_MAKE_TAGS
CF_INSTALL_OPTS

CF_WITHOUT_X

CF_WITH_MAN2HTML

### Compiler characteristics
CF_PROG_EXT
CF_LIB_PREFIX
AC_CONST
CF_XOPEN_SOURCE
CF_LARGEFILE

### initialize $ac_includes_default
AC_CHECK_DECL(exit)

AC_SYS_LONG_FILE_NAMES
CF_MIXEDCASE_FILENAMES
CF_LOCALE
AM_LANGINFO_CODESET

###	get special options
MAKE_SELFLINK=
AC_MSG_CHECKING(if you want a selflink for td_lib)
CF_ARG_DISABLE(selflink,
[  --disable-selflink      disable symlink above root used for local builds],
[use_selflink=no],[use_selflink=yes])
AC_MSG_RESULT($use_selflink)
test "$use_selflink" = no && MAKE_SELFLINK="#"
AC_SUBST(MAKE_SELFLINK)

CF_DISABLE_LEAKS
CF_DISABLE_ECHO
CF_ENABLE_WARNINGS

AC_ARG_ENABLE(debug,
[  --enable-debug          turn on debug (-g) (for testing/porting)],
[WithDebug=$enableval], [WithDebug=no])
AC_ARG_ENABLE(prototypes,
[  --enable-prototypes     turn on all prototypes (for testing only)],
[WithPrototypes=$enableval], [WithPrototypes=no])

###	undo the -g option of CFLAGS unless we've enabled it
if test -z "$cf_CFLAGS"; then
	if test "$WithDebug" = "no"; then
		CF_STRIP_G_OPT(CFLAGS)
	fi
fi

###	extra things that we'll substitute in the makefile
CF_BUILD_DIR
CF_TOP_SRCDIR
AC_SUBST(INCLUDES)
CF_MAKEFLAGS
CF_MAKE_AR_RULES
CF_MAKE_INCLUDE

CF_VERSION_INFO
CF_ENABLE_PC_FILES

dnl These are standard among *NIX systems, but not when cross-compiling
AC_CHECK_TOOL(RANLIB, ranlib, ':')
AC_CHECK_TOOL(LD, ld, ld)
AC_CHECK_TOOL(AR, ar, ar)
AC_CHECK_TOOL(NM, nm, nm)
CF_AR_FLAGS

### Headers
AC_HEADER_DIRENT
AC_MAJOR_HEADER
AC_RETSIGTYPE
CF_TD_SIG_ARGS
CF_ANSI_CPP
CF_STAT_ST_BLOCKS

AC_HAVE_HEADERS(time.h sys/time.h)
AC_HEADER_TIME

AC_HAVE_HEADERS(\
fcntl.h \
file.h \
grp.h \
ioctl.h \
limits.h \
pwd.h \
sgtty.h \
stdarg.h \
sys/fcntl.h \
sys/file.h \
sys/ioctl.h \
sys/param.h \
termio.h \
termios.h \
utime.h \
varargs.h \
)

# Check for curses libraries, but after getting them, split off the pieces so
# we don't pick up bogus dependencies on shared libraries in the programs that
# don't need curses.
cf_save_NCURSES_ETC="$LIBS"
CF_WITH_NCURSES_ETC
CF_CURSES_TERM_H
CF_CURSES_TERMCAP

if test "$LIBS" != "$cf_save_NCURSES_ETC" ; then
	CURSES_LIBS=`echo ".$LIBS" | sed -e "s%.%%" -e "s%$cf_save_NCURSES_ETC\$%%"`
fi
AC_SUBST(CURSES_LIBS)

CF_CHECK_REGEX
CF_VARARGS

### DataTypes
AC_CHECK_TYPE(daddr_t, unsigned long)
AC_CHECK_TYPE(dev_t, unsigned short)
AC_CHECK_TYPE(ino_t, unsigned short)
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_UID_T
AC_TYPE_GETGROUPS
AC_FUNC_VFORK
CF_WAIT
CF_SIZE_T

### DataItems/Structs
CF_SYS_ERRLIST
CF_GMTOFF

# We're done looking for libraries and header files.  Find the functions and
# datatypes

dnl Functions (use CF_HAVE_FUNCS for debugging, AC_HAVE_FUNCS for quick config)
if test "$WithWarnings" = yes; then
CF_HAVE_FUNCS(
_exit \
_filbuf \
_flsbuf \
calloc \
chgrp \
closedir \
creat \
exit \
fclose \
fflush \
fgetc \
fprintf \
fputc \
fputs \
fread \
free \
fscanf \
fseek \
fwrite \
getenv \
getgid \
getuid \
ioctl \
malloc \
open \
opendir \
pclose \
perror \
popen \
printf \
puts \
readdir \
readlink \
realloc \
rewind \
rewinddir \
seekdir \
sprintf \
sscanf \
strtok \
strtol \
symlink \
system \
telldir \
time \
ungetc \
)
fi

# Functions whose return type is important, or whose existence must be known
# to drive ifdefs.
CF_FUNC_LSTAT
CF_CURSES_FUNCS( \
_nc_free_and_exit \
addchnstr \
beep \
cbreak \
erasechar \
exit_curses \
getbegx \
getbegy \
getbegyx \
getmaxx \
getmaxy \
getmaxyx \
killchar \
newterm \
putp \
tigetstr \
ungetch \
win_wch \
winch \
winnstr \
wresize \
wscrl \
wsetscrreg \
)
CF_CURSES_DATA
CF_CURSES_MOUSE
CF_HAVE_FUNCS(
bzero \
chown \
endgrent \
endpwent \
execvp \
fork \
getcwd \
getegid \
geteuid \
getgrent \
getgrgid \
getgrnam \
getgroups \
gethostname \
getopt \
getpwent \
getpwnam \
getpwuid \
gettimeofday \
getwd \
has_colors \
intrflush \
is_xterm \
keypad \
link \
mkstemp \
mktemp \
mktime \
putenv \
realpath \
rename \
resizeterm \
setbuf \
setegid \
seteuid \
setgrent \
setlinebuf \
setpwent \
setrgid \
setruid \
setvbuf \
strchr \
strrchr \
tcgetattr \
tgetent \
tgetnum \
toascii \
tolower \
touchwin \
toupper \
ttyname \
typeahead \
use_default_colors \
utime \
vfork \
wait \
)
CF_STRERROR
CF_MKSTEMP

# work around some gcc built-ins
CF_CHECK_FUNCDECL([#include <string.h>],memmove,[AC_DEFINE(HAVE_MEMMOVE,1,[Define to 1 if we have memmove])])
CF_GETOPT_HEADER 

### Program locations
CF_PROGRAM_FULLPATH(DEFAULT_EDITOR, $EDITOR vi vile ed vim)
CF_PROGRAM_FULLPATH(DEFAULT_BROWSE, $BROWSE view [vile -V] ed)
CF_PROGRAM_FULLPATH(DEFAULT_PAGER, $PAGER more less most pg)

CF_TD_SRC_MODULES
CF_TD_TEST_MODULES

### Chop the curses libraries out of the list.  (We may get into trouble with
### the -L option, or order dependencies on some obscure systems).
if test -n "$CURSES_LIBS" ; then
	LIBS=`echo ".$LIBS" | sed -e "s%.%%" -e "s%$CURSES_LIBS%%"`
fi

CF_LD_SEARCHPATH
CF_TRIM_LDFLAGS

AC_OUTPUT( \
	td_make \
	makefile \
	include/makefile \
	src/makefile \
	support/makefile \
	support/gen-pkgconfig \
	$cf_cv_src_makefiles \
	$cf_cv_test_makefiles \
	test/makefile,[
mkdir -p support
CF_OUTPUT_IF_CHANGED(td_make, support/td_lib.mk)
CF_TD_SRC_MAKEFILES
CF_TD_TEST_MAKEFILES
mv man2html.tmp support/man2html.sh
],[
	AWK="$AWK"
	verbose="$verbose"
	cf_cv_ar_rules="$cf_cv_ar_rules"
	cf_cv_src_modules="$cf_cv_src_modules"
	cf_cv_test_modules="$cf_cv_test_modules"
	make_include_left="$make_include_left"
	make_include_right="$make_include_right"
],sort -u)
