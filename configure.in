dnl Process this file with 'autoconf' to produce a 'configure' script
dnl $Id: configure.in,v 12.86 1998/02/15 17:46:31 tom Exp $
AC_REVISION($Revision: 12.86 $)
AC_PREREQ(2.12.971230)
AC_INIT(include/td_lib.h)
AC_CONFIG_HEADER(include/td_config.h:include/config.hin)
CF_CHECK_CACHE

cf_CFLAGS="$CFLAGS"

###	get special options
CF_DISABLE_ECHO
AC_ARG_ENABLE(debug,
[  --enable-debug          turn on debug (-g) (for testing/porting)],
[WithDebug=$enableval], [WithDebug=no])
AC_ARG_ENABLE(warnings,
[  --enable-warnings       turn on GCC warnings (for testing/porting)],
[WithWarnings=$enableval], [WithWarnings=no])
AC_ARG_ENABLE(prototypes,
[  --enable-prototypes     turn on all prototypes (for testing only)],
[WithPrototypes=$enableval], [WithPrototypes=no])
AC_ARG_WITH(ncurses,
[  --with-ncurses          check for ncurses (a SysVr4 curses clone)],
[WithNcurses=$withval],  [WithNcurses=no])

###	program paths
AC_PROG_CC
AC_PROG_CPP
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET
CF_RCS_SCCS
AC_CHECK_PROG(OnHost, uname, [`uname -a`])
AC_CHECK_PROGS(LINT, tdlint lint alint, [echo lint not available])

AC_SYS_LONG_FILE_NAMES

###	undo the -g option of CFLAGS unless we've enabled it
if test -z "$cf_CFLAGS"; then
	if test "$WithDebug" = "no"; then
		CF_STRIP_G_OPT(CFLAGS)
	fi
fi

###	extra things that we'll substitute in the makefile
CF_TOP_SRCDIR
AC_SUBST(INCLUDES)
CF_MAKEFLAGS
CF_MAKE_AR_RULES
CF_MAKE_INCLUDE

###	use option -with-warnings to turn on all gcc warnings
if test "$WithWarnings" = yes; then
	CF_GCC_WARNINGS
fi
CF_GCC_ATTRIBUTES

### Compiler characteristics
AC_CONST
CF_ANSI_CC_CHECK

### Headers
AC_STDC_HEADERS
AC_HEADER_DIRENT
AC_MAJOR_HEADER
AC_RETSIGTYPE
CF_TD_SIG_ARGS
CF_ANSI_CPP
CF_STAT_ST_BLOCKS

AC_HAVE_HEADERS(time.h sys/time.h)
AC_HEADER_TIME

AC_HAVE_HEADERS(stdlib.h unistd.h limits.h string.h memory.h grp.h pwd.h)
AC_HAVE_HEADERS(stdarg.h varargs.h)
AC_HAVE_HEADERS(file.h fcntl.h ioctl.h)
dnl don't combine <file.h> and <sys/file.h>, because autoconf gets confused
AC_HAVE_HEADERS(sys/file.h sys/fcntl.h sys/ioctl.h sys/param.h)
AC_HAVE_HEADERS(termcap.h termios.h termio.h sgtty.h utime.h)

AC_CHECK_LIB(bsd, bcopy)

# Check for curses libraries, but after getting them, split off the pieces so
# we don't pick up bogus dependencies on shared libraries in the programs that
# don't need curses.
td_save_LIBS="$LIBS"
CURSES_LIBS=""
if test $WithNcurses = no ; then
	CF_CURSES_LIBS
else
	CF_NCURSES_LIBS
	CF_NCURSES_CPPFLAGS
fi
if test "$LIBS" != "$td_save_LIBS" ; then
	CURSES_LIBS=`echo ".$LIBS" | sed -e "s%.%%" -e "s%$td_save_LIBS\$%%"`
fi
AC_SUBST(CURSES_LIBS)

CF_CHECK_REGEX
CF_VARARGS

### DataTypes
AC_CHECK_TYPE(dev_t, unsigned short)
AC_CHECK_TYPE(ino_t, unsigned short)
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_UID_T
AC_TYPE_GETGROUPS
AC_VFORK
CF_WAIT
CF_SIZE_T
CF_CURSES_DATA

### DataItems/Structs
CF_SYS_ERRLIST
CF_GMTOFF

# We're done looking for libraries and header files.  Find the functions and
# datatypes

dnl Functions (use CF_HAVE_FUNCS for debugging, AC_HAVE_FUNCS for quick config)
if test $WithWarnings = yes; then
CF_HAVE_FUNCS(
_exit \
_filbuf \
_flsbuf \
calloc \
closedir \
creat \
exit \
fclose \
fflush \
fgetc \
fprintf \
fputc \
fputs \
fread \
free \
fscanf \
fseek \
fwrite \
getenv \
getgid \
getuid \
ioctl \
malloc \
open \
opendir \
pclose \
perror \
popen \
printf \
puts \
readdir \
readlink \
realloc \
rewind \
rewinddir \
seekdir \
sprintf \
sscanf \
strtok \
strtol \
symlink \
system \
telldir \
time \
ungetc \
)
fi

# Functions whose return type is important, or whose existence must be known
# to drive ifdefs.
CF_FUNC_LSTAT
CF_CURSES_FUNCS( \
addchnstr \
beep \
cbreak \
erasechar \
killchar \
ungetch \
winnstr \
winchstr \
wresize \
wscrl \
wsetscrreg \
)
CF_CURSES_MOUSE
CF_HAVE_FUNCS(
bzero \
endgrent \
endpwent \
execvp \
fork \
getcwd \
getegid \
geteuid \
getgrent \
getgrgid \
getgrnam \
getgroups \
getopt \
getpwent \
getpwnam \
getpwuid \
gettimeofday \
getwd \
has_colors \
intrflush \
is_xterm \
keypad \
memmove \
mkstemp \
mktemp \
putenv \
realpath \
rename \
resizeterm \
setbuf \
setegid \
setgrent \
setlinebuf \
setpwent \
setrgid \
setruid \
setvbuf \
strchr \
strerror \
strrchr \
tcgetattr \
tgetent \
tgetnum \
tolower \
touchwin \
toupper \
typeahead \
use_default_colors \
utime \
vfork \
wait \
)

### Program locations
CF_PROGRAM_FULLPATH(DEFAULT_EDITOR, $EDITOR vi)
CF_PROGRAM_FULLPATH(DEFAULT_BROWSE, $BROWSE view)
CF_PROGRAM_FULLPATH(DEFAULT_PAGER, $PAGER more less most)

CF_TD_SRC_MODULES
CF_TD_TEST_MODULES

### Chop the curses libraries out of the list.  (We may get into trouble with
### the -L option, or order dependencies on some obscure systems).
if test -n "$CURSES_LIBS" ; then
	LIBS=`echo ".$LIBS" | sed -e "s%.$CURSES_LIBS%%"`
fi

AC_OUTPUT( \
	td_make \
	makefile \
	src/makefile \
	$cf_cv_src_makefiles \
	$cf_cv_test_makefiles \
	test/makefile,[
CF_OUTPUT_IF_CHANGED(td_make, support/td_lib.mk)
CF_TD_SRC_MAKEFILES
CF_TD_TEST_MAKEFILES
],[
	AWK="$AWK"
	verbose="$verbose"
	cf_cv_ar_rules="$cf_cv_ar_rules"
	cf_cv_src_modules="$cf_cv_src_modules"
	cf_cv_test_modules="$cf_cv_test_modules"
	make_include_left="$make_include_left"
	make_include_right="$make_include_right"
],sort)
